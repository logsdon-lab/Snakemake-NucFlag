include: "rules/common.smk"


SAMPLE_ASSEMBLIES, SAMPLE_READS = get_sample_assemblies_and_reads()
SAMPLE_INFO = {sm["name"]: sm for sm in config["samples"]}

TMP_DIR = config.get("tmp_dir", os.environ.get("TMPDIR", "/tmp"))
LOGS_DIR = config.get("logs_dir", "logs/nucflag")
BMKS_DIR = config.get("benchmarks_dir", "benchmarks/nucflag")
ALIGNERS = {"winnowmap", "pbmm2", "minimap2"}
ALIGNER = config.get("aligner", "winnowmap")
ENV_YAML = f"env/{ALIGNER}.yaml"


def get_asm(wc):
    return (
        os.path.join(config["output_dir"], f"{wc.sm}_merged_dedup_asm.fa")
        if isinstance(SAMPLE_ASSEMBLIES[str(wc.sm)], dict)
        else SAMPLE_ASSEMBLIES[str(wc.sm)]
    )


if ALIGNER not in ALIGNERS:
    raise ValueError(f"Invalid aligner option. ({ALIGNER})")


include: "rules/winnowmap.smk" if ALIGNER == "winnowmap" else "rules/minimap2.smk"


wildcard_constraints:
    sm="|".join(SAMPLE_ASSEMBLIES.keys()),


rule merge_asm_files:
    input:
        asm_files=lambda wc: ancient(SAMPLE_ASSEMBLIES[str(wc.sm)].values()),
    output:
        merged_asm=temp(os.path.join(config["output_dir"], "{sm}_merged_asm.fa")),
        asm=os.path.join(config["output_dir"], "{sm}_merged_dedup_asm.fa"),
    conda:
        ENV_YAML
    resources:
        mem=8,
    log:
        os.path.join(LOGS_DIR, "merge_{sm}_asm_files.log"),
    shell:
        """
        for file in {input.asm_files}; do
            if [[ $file == *.gz ]]; then
                zcat $file >> {output.merged_asm}
            else
                cat $file >> {output.merged_asm}
            fi
        done
        seqkit rmdup {output.merged_asm} > {output.asm} 2> {log}
        """


def get_aln_to_asm(wc) -> list[str]:
    sm_reads = SAMPLE_READS[str(wc.sm)]
    samples = [str(wc.sm)] * len(sm_reads)

    alns = set(
        expand(rules.align_reads_to_asm.output, zip, sm=samples, id=sm_reads.keys())
    )
    if not alns:
        raise FileNotFoundError(f"No reads found for sample: {SAMPLE_INFO[str(wc.sm)]}")
    return ancient(alns)


rule merge_hifi_read_asm_alignments:
    input:
        get_aln_to_asm,
    output:
        alignment=os.path.join(config["output_dir"], "{sm}_hifi.bam"),
        alignment_idx=os.path.join(config["output_dir"], "{sm}_hifi.bam.bai"),
    threads: config["threads_aln"]
    resources:
        mem=config["mem_aln"],
        sort_mem="4G",
    params:
        tmp_dir=config.get("tmp_dir", os.environ.get("TMPDIR", "/tmp")),
    conda:
        ENV_YAML
    log:
        os.path.join(LOGS_DIR, "merge_{sm}_hifi_read_asm_alignments.log"),
    benchmark:
        os.path.join(BMKS_DIR, "merge_{sm}_hifi_read_asm_alignments.tsv")
    shell:
        """
        {{ samtools merge -@ {threads} - {input} | \
        samtools sort -T {params.tmp_dir} -m {resources.sort_mem} -@ {threads} -;}} > {output.alignment} 2> {log}
        samtools index {output.alignment} 2>> {log}
        """


rule check_asm_nucflag:
    input:
        bam_file=ancient(rules.merge_hifi_read_asm_alignments.output.alignment),
        # Optional arguments.
        regions=lambda wc: SAMPLE_INFO[str(wc.sm)].get("region_bed", []),
        config=lambda wc: SAMPLE_INFO[str(wc.sm)].get("config", []),
        ignore_regions=lambda wc: SAMPLE_INFO[str(wc.sm)].get("ignore_bed", []),
        overlay_regions=lambda wc: SAMPLE_INFO[str(wc.sm)].get("overlay_beds", []),
    output:
        plot_dir=directory(os.path.join(config["output_dir"], "{sm}")),
        cov_dir=(
            directory(os.path.join(config["output_dir"], "{sm}_coverage"))
            if config.get("output_coverage")
            else []
        ),
        misassemblies=os.path.join(
            config["output_dir"],
            "{sm}_cen_misassemblies.bed",
        ),
        asm_status=os.path.join(
            config["output_dir"],
            "{sm}_cen_status.bed",
        ),
    params:
        regions=lambda wc, input: f"-b {input.regions}" if input.regions else "",
        config=lambda wc, input: f"-c {input.config}" if input.config else "",
        ignore_regions=lambda wc, input: (
            f"--ignore_regions {input.ignore_regions}" if input.ignore_regions else ""
        ),
        overlay_regions=lambda wc, input: (
            f"--overlay_regions {' '.join(input.overlay_regions)}"
            if input.overlay_regions
            else ""
        ),
        output_coverage=lambda wc, output: (
            f"--output_cov_dir {output.cov_dir}"
            if config.get("output_coverage")
            else ""
        ),
    threads: config["processes_nucflag"]
    conda:
        "env/nucflag.yaml"
    resources:
        mem=config["mem_nucflag"],
    log:
        os.path.join(LOGS_DIR, "run_nucflag_{sm}.log"),
    benchmark:
        os.path.join(BMKS_DIR, "run_nucflag_{sm}.tsv")
    shell:
        """
        nucflag \
        -i {input.bam_file} \
        -d {output.plot_dir} \
        -o {output.misassemblies} \
        -t {threads} \
        -p {threads} \
        -s {output.asm_status} \
        {params.config} \
        {params.regions} \
        {params.ignore_regions} \
        {params.overlay_regions} \
        {params.output_coverage} &> {log}
        """


rule nucflag:
    input:
        expand(rules.check_asm_nucflag.output, sm=SAMPLE_ASSEMBLIES.keys()),
    default_target: True
